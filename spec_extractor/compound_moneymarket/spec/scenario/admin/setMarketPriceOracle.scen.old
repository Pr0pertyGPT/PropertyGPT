
-- Tests for MoneyMarket's `_setMarketPriceOracle(address,address)` function

Macro ListAsset asset
    SupportMarket asset (FixedPrice 1.0) (FixedRate 0.5 0.75) SimplePolicyHook
    Assert Success

Test "is initially unset"
    AddToken Ether
    Assert Equal (GetMarketPriceOracle Ether) (Exactly 0)

Test "fails if not called by admin"
    AddToken Ether
    Invariant Static (GetMarketPriceOracle Ether)
    From Geoff (SetMarketPriceOracle Ether Simple)
    Assert Failure UNAUTHORIZED SET_ORACLE_OWNER_CHECK

Test "fails if market not listed"
    AddToken Ether
    Invariant Static (GetMarketPriceOracle Ether)
    SetMarketPriceOracle Ether Simple
    Assert Failure MARKET_NOT_LISTED SET_ORACLE_MARKET_NOT_LISTED

Test "reverts if new address is not really an oracle"
    AddToken Ether
    ListAsset Ether
    Invariant Static (GetMarketPriceOracle Ether)
    SetMarketPriceOracle Ether NotPriceOracle
    Assert Revert

Test "successfully set by admin"
    AddToken Ether
    ListAsset Ether
    SetMarketPriceOracle Ether Simple
    Assert Success
    Assert Equal (GetMarketPriceOracle Ether) LastContract
    Assert Log "NewMarketOracle" \
        ("asset" (Address Ether)) ("oldPriceOracle" Anything) ("newPriceOracle" (LastContract))
