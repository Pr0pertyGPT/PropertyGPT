
-- Tests for MoneyMarket's `_setMarketPolicyHook(address,address)` function

Macro ListAsset asset
	SupportMarket asset (FixedPrice 1.0) (FixedRate 0.5 0.75) SimplePolicyHook
	Assert Success

Test "emits log on failure due to non-admin caller"
	AddToken Ether
	Invariant Static (GetMarketPolicyHook Ether)
	From Geoff (SetMarketPolicyHook Ether SimplePolicyHook)
	Assert Failure UNAUTHORIZED SET_POLICY_HOOK_OWNER_CHECK

Test "emits log on failure due to not-listed market"
	AddToken Ether
	Invariant Static (GetMarketPolicyHook Ether)
	SetMarketPolicyHook Ether SimplePolicyHook
	Assert Failure MARKET_NOT_LISTED SET_POLICY_HOOK_MARKET_NOT_LISTED

Test "emits log on failure due to being a bad policy hook"
	AddToken Ether
	ListAsset Ether
	Invariant Static (GetMarketPolicyHook Ether)
	SetMarketPolicyHook Ether FakePolicyHook
	Assert Failure BAD_INPUT SET_POLICY_HOOK_IS_NOT_POLICY_HOOK

Test "reverts due to not being a policy hook"
	AddToken Ether
	ListAsset Ether
	Invariant Static (GetMarketPolicyHook Ether)
	SetMarketPolicyHook Ether NotPolicyHook
	Assert Revert

Test "successfully sets policy hook"
	AddToken Ether
	ListAsset Ether
	SetMarketPolicyHook Ether SimplePolicyHook
	Assert Success
	Assert Equal (GetMarketPolicyHook Ether) LastContract
	Assert Log "PolicyHookChanged" \
		("asset" (Address Ether)) ("oldPolicyHook" Anything) ("newPolicyHook" (LastContract))
